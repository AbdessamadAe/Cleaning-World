<program> ::= <world_def> <function_list_opt> <agent_def>

<function_list_opt> ::= ε | <function_list>
<function_list> ::= <function_decl>

<function_decl> ::= FUNC ID '(' <param_list_opt> ')' RETURNS <type> '{' <stmt_list> '}'
<param_list_opt> ::= ε | <param_list>
<param_list> ::= <param_decl> | <param_decl> ',' <param_list>
<param_decl> ::= ID

<type> ::= TYPE_INT | TYPE_VOID

<world_def> ::= WORLD ID '{' <world_body> '}'
<world_body> ::= <world_stmt> | <world_stmt> <world_body>
<world_stmt> ::= SIZE '(' INT_LIT ',' INT_LIT ')' ';'
               | ENTRY '(' INT_LIT ',' INT_LIT ',' DIR ')' ';'
               | EXIT '(' INT_LIT ',' INT_LIT ')' ';'
               | OBSTACLE_DEF '(' INT_LIT ',' INT_LIT ')' ';'
               | DIRT_DEF '(' INT_LIT ',' INT_LIT ')' ';'

<agent_def> ::= AGENT ID '{' <stmt_list> '}'

<stmt_list> ::= <stmt> | <stmt> <stmt_list>

<stmt> ::= VAR ID ASSIGN <expr> ';'
         | ID ASSIGN <expr> ';'
         | IF <condition> THEN <stmt_list> ELSE <stmt_list> ENDIF ';'
         | WHILE <condition> DO <stmt_list> ENDWHILE ';'
         | MOVE ';'
         | TURN <turn_dir> ';'
         | CLEAN ';'
         | BACKTRACK ';'
         | REPORT <expr> ';'
         | RETURN <expr> ';'
         | <function_call> ';'

<turn_dir> ::= LEFT | RIGHT

<function_call> ::= ID '(' <arg_list_opt> ')'
<arg_list_opt> ::= ε | <arg_list>
<arg_list> ::= <expr> | <expr> ',' <arg_list>

<condition> ::= SENSE <sense_expr>
              | NOT <condition>
              | <condition> AND <condition>
              | <condition> OR <condition>
              | <expr> <relop> <expr>
              | UNVISITED

<sense_expr> ::= DIRT | OBSTACLE | EXIT | ENTRY

<relop> ::= EQ | NEQ | LT | GT

<expr> ::= <term> | <term> PLUS <expr> | <term> MINUS <expr>
<term> ::= ID | INT_LIT | <function_call>

<dir> ::= N | E | S | W
